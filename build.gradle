buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${ANDROID_GRADLE_VERSION}"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:${COVERALLS_GRADLE_VERSION}"
    }
}

subprojects { project ->
    repositories {
        jcenter()
        google()
    }

    apply plugin: 'checkstyle'

    checkstyle {
        configFile = new File(rootDir, 'checkstyle.xml')
        configProperties.checkStyleConfigDir = rootProject.rootDir
    }

    task checkstyle(type: Checkstyle) {
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'

        // empty classpath
        classpath = files()
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            if (!name.contains('Test')) {
                options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
            }
        }
    }
}

repositories {
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

wrapper {
    gradleVersion = '2.6'
}
